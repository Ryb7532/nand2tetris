// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char press, pressed;
        do Output.printChar(0);
        let pressed = 0;
        while ((pressed=0) | (press>0)) {
            let press = Keyboard.keyPressed();
            if (press > 0) {
                let pressed = press;
            }
        }
        do Output.printChar(String.backSpace());
        do Output.printChar(pressed);
        return pressed;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char c, newline, backspace;
        var String str;
        var boolean flag;
        let newline = String.newLine();
        let backspace = String.backSpace();
        let str = String.new(80);
        do Output.printString(message);
        let flag = true;
        while (flag) {
            let c = Keyboard.readChar();
            let flag = ~(c = newline);
            if (flag) {
                if (c = backspace) {
                    do str.eraseLastChar();
                } else {
                    do str.appendChar(c);
                }
            }
        }
        return str;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.
     */
    function int readInt(String message) {
        var String str;
        var int val;
        let str = Keyboard.readLine(message);
        let val = str.intValue();
        do str.dispose();
        return val;
    }
}
